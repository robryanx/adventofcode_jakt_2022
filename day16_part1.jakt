/// Expect:
/// - output: "2087\n"

import reader
import utility

struct Valve {
    rate: usize
    valves: [String]
}

struct ValveConnection {
    name: String
    distance: usize
    rate: usize
}

struct ValveValue {
    name: String
    distance: usize
    value: usize
}

fn main() {
    mut valves: [String:Valve] = [:]

    // Valve AA has flow rate=0; tunnels lead to valves DD, II, BB
    for line in reader::lines_string(day: 16, examples: false) {
        let separator = match line.contains(" tunnels lead to valves ") {
            true => " tunnels lead to valves "
            else => " tunnel leads to valve "
        }

        let parts = utility::split_on_string(input: line, separator)
        let all_parts = line.split(c' ')

        valves[all_parts[1]] = Valve(
            rate: format_match(line: all_parts[4], format: "rate=%;")![0] as! usize
            valves: utility::list(parts[1])
        )
    }

    mut all_valve_connections: [String:[ValveConnection]] = [:]
    for (key, val) in valves {
        if val.rate > 0 or key == "AA" {
            all_valve_connections[key] = get_valve_connections(&valves, from: key)
        }
    }

    mut visited: [String:usize] = [:]
    let total = recurse_values(&all_valve_connections, current: "AA", remaining: 30, total: 0, value: 0, visited)

    println("{}", total)
}

fn recurse_values(all_valve_connections: &[String:[ValveConnection]], current: String, remaining: usize, mut total: usize, value: usize, visited: [String:usize]) throws -> usize {
    mut new_visited: [String:usize] = [:]
    new_visited.ensure_capacity(visited.size()+1)
    for (key, val) in visited {
        new_visited[key] = val
    }
    new_visited[current] = value

    total += value
    
    let valve_connections = all_valve_connections[current]

    // apply values to valves
    mut valve_values: [ValveValue] = []
    valve_values.ensure_capacity(valve_connections.size())
    for i in 0..valve_connections.size() {
        if valve_connections[i].distance + 1 < remaining and not new_visited.contains(valve_connections[i].name) {
            valve_values.push(ValveValue(
                name: valve_connections[i].name
                distance: valve_connections[i].distance
                value: (remaining - valve_connections[i].distance) * valve_connections[i].rate
            ))
        }
    }

    if valve_values.size() > 1 {
        utility::sort(values: valve_values, sorter: &fn(anon item_a: &ValveValue, anon item_b: &ValveValue) -> i8 {
            if item_a.value > item_b.value {
                return -1
            } else if item_a.value < item_b.value {
                return 1
            }

            return 0
        })
    }

    if valve_values.size() > 0 {
        mut largest = 0uz
        for i in 0uz..8uz {
            if valve_values.size() > i {
                let check_largest = recurse_values(all_valve_connections, current: valve_values[i].name, remaining: remaining - valve_values[i].distance, total, value: valve_values[i].value, visited: new_visited)
                if check_largest > largest {
                    largest = check_largest
                }
            }
        }

        if largest > 0 {
            total = largest
        }
    }

    return total    
}

fn get_valve_connections(valves: &[String:Valve], from: String) throws -> [ValveConnection] {
    // find the best path from the current valve to each valve with a flow rate>0
    mut valve_connections: [ValveConnection] = []
    valve_connections.ensure_capacity(valves.size())

    for (key, val) in *valves {
        if val.rate > 0 and key != from {
            mut visited: [String] = []
            let path_value = find_path(valves, from, to: key, visited, length: 0)
            valve_connections.push(ValveConnection(
                name: key
                distance: (path_value.0 + 1)
                rate: val.rate
            ))
        }
    }

    return valve_connections
}

fn find_path(valves: &[String:Valve], from: String, to: String, visited: [String], length: usize) throws -> (usize, [String]) {
    if from == to {
        return (length, visited)
    }

    mut new_visited: [String] = visited[..].to_array()
    new_visited.push(from)

    mut to_visit = false
    mut shortest = 10000uz
    mut shortest_visited: [String] = []
    for valve in valves[from].valves {
        if not new_visited.contains(valve) {
            to_visit = true

            let (check_length, check_visited) = find_path(valves, from: valve, to, visited: new_visited, length: length+1)
            if check_length != 0 and check_length < shortest {
                shortest = check_length
                shortest_visited = check_visited
            }
        }
    }

    if not to_visit or shortest == 10000 {
        return (0uz, visited)
    } else {
        return (shortest, shortest_visited)
    }
}
