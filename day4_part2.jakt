/// Expect:
/// - output: "928\n"

import reader
import utility

struct Assignment {
    start: i32
    end: i32
}

function main() {
    mut total = 0

    for assignment_pair in reader::lines_string(day: 4) {
        let assignments = build_assignments(assignments_str: assignment_pair.split(','))

        if assignments_intesect(assignments) {
            total++
        }
    }

    println("{}", total)
}

function build_assignments(assignments_str: [String]) throws -> [Assignment] {
    mut assignment_list: [Assignment] = []

    for assignment in assignments_str {
        let assignment_parts = assignment.split('-')
        assignment_list.push(Assignment(
            start: assignment_parts[0].to_int()!
            end: assignment_parts[1].to_int()!
        ))
    }

    return assignment_list
}

function assignments_intesect(assignments: [Assignment]) -> bool => 
    (assignments[0].end >= assignments[1].start and assignments[0].end <= assignments[1].end) or 
    (assignments[1].end >= assignments[0].start and assignments[1].end <= assignments[0].end)